"""
  TensorProductSpace{
      manifold_dim, num_components, num_patches, num_spaces, T, CIE, LIE, CIB, LIB
  } <: AbstractFESpace{manifold_dim, num_components, num_patches}

A structure representing a `TensorProductSpace`, defined by the tensor product of
`num_spaces` constituent spaces. The resulting tensor product has a `manifold_dim` equal to
the sum of the constituent spaces' manifold dimensions.

# Fields
- `constituent_spaces::T`: A tuple of constituent finite element spaces to be tensored.
- `cart_num_elements::CIE`: To convert from tensor-product indexing to constituent-wise
  indexing for elements.
- `lin_num_elements::LIE`: To convert from constituent-wise indexing to tensor-product
  indexing for elements.
- `cart_num_basis::CIB`: To convert from tensor-product indexing to constituent-wise
  indexing for basis functions.
- `lin_num_basis::LIB`: To convert from constituent-wise indexing to tensor-product indexing
  for basis functions.
- `dof_partition::Vector{Vector{Vector{Int}}}`: See [`get_dof_partition`](@ref).
"""
struct TensorProductSpace{
    manifold_dim, num_components, num_patches, num_spaces, T, CIE, LIE, CIB, LIB
} <: AbstractFESpace{manifold_dim, num_components, num_patches}
    constituent_spaces::T
    cart_num_elements::CIE
    lin_num_elements::LIE
    cart_num_basis::CIB
    lin_num_basis::LIB
    dof_partition::Vector{Vector{Vector{Int}}}

    function TensorProductSpace(
        constituent_spaces::T
    ) where {num_spaces, T <: NTuple{num_spaces, AbstractFESpace}}
        if all(get_num_components.(constituent_spaces) .== 1)
            num_components = 1
        else
            throw(ArgumentError("All spaces must have only one component."))
        end

        # Tensor-product space
        manifold_dim = sum(get_manifold_dim, constituent_spaces)
        num_patches = prod(get_num_patches, constituent_spaces)
        # Pre-allocate memory for degree of freedom partitioning
        dof_partition = Vector{Vector{Vector{Int}}}(undef, num_patches)
        # Constituent spaces
        const_dof_partitions = ntuple(
            space -> get_dof_partition(constituent_spaces[space]), num_spaces
        )
        const_num_patches = ntuple(space -> length(const_dof_partitions[space]), num_spaces)
        const_num_basis = ntuple(
            space -> get_num_basis(constituent_spaces[space]), num_spaces
        )
        const_num_elements = ntuple(
            space -> get_num_elements(constituent_spaces[space]), num_spaces
        )
        cart_num_elements = CartesianIndices(const_num_elements)
        lin_num_elements = LinearIndices(const_num_elements)
        cart_num_basis = CartesianIndices(const_num_basis)
        lin_num_basis = LinearIndices(const_num_basis)
        # Loop over all spaces and build the appropriate index subsets
        for (patch_count, patch_ids) in enumerate(CartesianIndices(const_num_patches))
            const_patches = ntuple(
                space -> const_dof_partitions[space][patch_ids[space]], num_spaces
            )
            const_partition_lengths = ntuple(
                space -> length(const_patches[space]), num_spaces
            )
            dof_partition[patch_count] = Vector{Vector{Int}}(
                undef, prod(const_partition_lengths)
            )
            for (partition_count, partition_ids) in
                enumerate(CartesianIndices(const_partition_lengths))
                const_partition_dofs = ntuple(
                    space -> const_patches[space][partition_ids[space]], num_spaces
                )
                dof_partition[patch_count][partition_count] = Vector{Int}(
                    undef, prod(length, const_partition_dofs)
                )
                for (dof_count, const_basis_id) in
                    enumerate(Iterators.product(const_partition_dofs...))
                    basis_id = lin_num_basis[const_basis_id...]
                    dof_partition[patch_count][partition_count][dof_count] = basis_id
                end
            end
        end

        return new{
            manifold_dim,
            num_components,
            num_patches,
            num_spaces,
            T,
            typeof(cart_num_elements),
            typeof(lin_num_elements),
            typeof(cart_num_basis),
            typeof(lin_num_basis),
        }(
            constituent_spaces,
            cart_num_elements,
            lin_num_elements,
            cart_num_basis,
            lin_num_basis,
            dof_partition,
        )
    end
end

# Getters

get_cart_num_basis(space::TensorProductSpace) = space.cart_num_basis
get_lin_num_basis(space::TensorProductSpace) = space.lin_num_basis
get_cart_num_elements(space::TensorProductSpace) = space.cart_num_elements
get_lin_num_elements(space::TensorProductSpace) = space.lin_num_elements
get_constituent_spaces(space::TensorProductSpace) = space.constituent_spaces
get_num_basis(space::TensorProductSpace) = prod(get_constituent_num_basis(space))

function get_num_elements(space::TensorProductSpace)
    return prod(get_constituent_num_elements(space))
end

"""
    get_num_spaces(
        ::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns the number of constituent spaces in a given `TensorProductSpace`.
"""
function get_num_spaces(
    ::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
) where {manifold_dim, num_components, num_patches, num_spaces}
    return num_spaces
end

"""
    get_constituent_space(space::TensorProductSpace, space_id::Int)

Returns the constituent space numbered `space_id` of the tensor product `space`.
"""
function get_constituent_space(space::TensorProductSpace, space_id::Int)
    return get_constituent_spaces(space)[space_id]
end

"""
    get_constituent_element_id(space::TensorProductSpace, element_id::Int)

Returns a tuple corresponding to the conversion of `element_id` in tensor-product numbering
to constituent-wise numbering.
"""
function get_constituent_element_id(space::TensorProductSpace, element_id::Int)
    return Tuple(get_cart_num_elements(space)[element_id])
end

"""
    get_constituent_basis_id(space::TensorProductSpace, basis_id::Int)

Returns a tuple corresponding to the conversion of `basis_id` in tensor-product numbering to
constituent-wise numbering.
"""
function get_constituent_basis_id(space::TensorProductSpace, basis_id::Int)
    return Tuple(get_cart_num_basis(space)[basis_id])
end

"""
    get_constituent_num_basis(space::TensorProductSpace)

Returns a tuple corresponding to the constituent-wise number of basis functions — or
dimension.
"""
function get_constituent_num_basis(space::TensorProductSpace)
    return Tuple(maximum(get_cart_num_basis(space)))
end

"""
    get_constituent_num_basis(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        element_id::Int,
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise number of basis functions supported on
`element_id`.
"""
function get_constituent_num_basis(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_element_id = get_constituent_element_id(space, element_id)
    const_num_basis = ntuple(
        space -> get_num_basis(const_spaces[space], const_element_id[space]), num_spaces
    )

    return const_num_basis
end

function get_num_basis(space::TensorProductSpace, element_id::Int)
    return prod(get_constituent_num_basis(space, element_id))
end

"""
    get_constituent_num_elements(space::TensorProductSpace)

Returns a tuple corresponding to the constituent-wise number of elements.
"""
function get_constituent_num_elements(space::TensorProductSpace)
    return Tuple(maximum(get_cart_num_elements(space)))
end

"""
    get_constituent_manifold_dim(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise manifold dimension.
"""
function get_constituent_manifold_dim(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)

    return ntuple(space -> get_manifold_dim(const_spaces[space]), num_spaces)
end

"""
    get_constituent_basis_indices(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        element_id::Int,
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise basis indices supported on
`element_id`.

See also [`get_basis_indices`](@ref).
"""
function get_constituent_basis_indices(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_element_id = get_constituent_element_id(space, element_id)
    const_basis_indices = ntuple(
        space -> get_basis_indices(const_spaces[space], const_element_id[space]), num_spaces
    )

    return const_basis_indices
end

"""
    get_constituent_support(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        basis_id::Int,
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise support of the basis function
identified by `basis_id`.

See also [`get_support`](@ref).
"""
function get_constituent_support(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    basis_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_basis_id = get_constituent_basis_id(space, basis_id)
    const_spaces = get_constituent_spaces(space)
    const_support = ntuple(
        space -> get_support(const_spaces[space], const_basis_id[space]), num_spaces
    )

    return const_support
end

"""
    get_constituent_extraction(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        element_id::Int,
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise extraction at `element_id`.

See also [`get_extraction`](@ref).
"""
function get_constituent_extraction(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_element_id = get_constituent_element_id(space, element_id)
    const_spaces = get_constituent_spaces(space)
    const_extraction = ntuple(
        space -> get_extraction(const_spaces[space], const_element_id[space]), num_spaces
    )

    return const_extraction
end

"""
    get_constituent_polynomial_degree(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise polynomial degree. Note that
`get_polynomial_degree` is not necessarily defined for every constituent space.

See also [`get_polynomial_degree`](@ref).
"""
function get_constituent_polynomial_degree(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_polynomial_degree = ntuple(
        space -> get_polynomial_degree(const_spaces[space]), num_spaces
    )

    return const_polynomial_degree
end

"""
    get_constituent_local_basis(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        element_id::Int,
        xi::NTuple{manifold_dim, Vector{Float64}},
        nderivatives::Int=0,
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise local basis evaluation at `element_id`
and points `xi`.

See also [`get_local_basis`](@ref).
"""
function get_constituent_local_basis(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
    xi::NTuple{manifold_dim, Vector{Float64}},
    nderivatives::Int=0,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_element_id = get_constituent_element_id(space, element_id)
    const_xi = get_constituent_evaluation_points(space, xi)
    const_local_basis = ntuple(
        space -> get_local_basis(
            const_spaces[space], const_element_id[space], const_xi[space], nderivatives
        ),
        num_spaces,
    )

    return const_local_basis
end

"""
    get_constituent_evaluations(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        element_id::Int,
        xi::NTuple{manifold_dim, Vector{Float64}},
        nderivatives::Int=0,
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Get evaluations of all constituent spaces at `element_id` and points `xi`.
"""
function get_constituent_evaluations(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
    xi::NTuple{manifold_dim, Vector{Float64}},
    nderivatives::Int=0,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_element_id = get_constituent_element_id(space, element_id)
    const_xi = get_constituent_evaluation_points(space, xi)
    const_eval = ntuple(
        space -> evaluate(
            const_spaces[space], const_element_id[space], const_xi[space], nderivatives
        )[1],
        num_spaces,
    )

    return const_eval
end

"""
    get_constituent_evaluation_points(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
        xi::NTuple{manifold_dim, Vector{Float64}},
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple corresponding to the constituent-wise splitting of `xi` according to each
constituent space's manifold dimension.
"""
function get_constituent_evaluation_points(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    xi::NTuple{manifold_dim, Vector{Float64}},
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_manifold_indices = get_constituent_manifold_indices(space)
    const_xi = ntuple(space -> xi[const_manifold_indices[space]], num_spaces)

    return const_xi
end

"""
    get_constituent_manifold_indices(
        space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
    ) where {manifold_dim, num_components, num_patches, num_spaces}

Returns a tuple of ranges corresponding to the constituent-wise splitting `manifold_dim`,
use to iterate over each manifold dimension of each constituent space.
"""
function get_constituent_manifold_indices(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces}
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_manifold_dim = get_constituent_manifold_dim(space)
    cum_const_manifold_dim = (0, cumsum(const_manifold_dim)...)
    const_manifold_indices = ntuple(
        space -> (cum_const_manifold_dim[space] + 1):cum_const_manifold_dim[space + 1],
        num_spaces,
    )

    return const_manifold_indices
end

function get_constituent_element_vertices(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_element_id = get_constituent_element_id(space, element_id)
    const_element_vertices = ntuple(
        space -> get_element_vertices(const_spaces[space], const_element_id[space]),
        num_spaces,
    )

    return const_element_vertices
end

function get_constituent_element_lengths(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_spaces = get_constituent_spaces(space)
    const_element_id = get_constituent_element_id(space, element_id)
    const_element_lengths = ntuple(
        space -> get_element_lengths(const_spaces[space], const_element_id[space]),
        num_spaces,
    )

    return const_element_lengths
end

function get_basis_indices(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_basis_indices = get_constituent_basis_indices(space, element_id)
    lin_num_basis = get_lin_num_basis(space)
    basis_indices = Vector{Int}(undef, prod(length, const_basis_indices))
    for (basis_count, const_basis_id) in
        enumerate(Iterators.product(const_basis_indices...))
        basis_indices[basis_count] = lin_num_basis[const_basis_id...]
    end

    return basis_indices
end

function get_support(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    basis_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_support = get_constituent_support(space, basis_id)
    lin_num_elements = get_lin_num_elements(space)
    support = Vector{Int}(undef, prod(length, const_support))
    for (element_count, const_element_id) in enumerate(Iterators.product(const_support...))
        support[element_count] = lin_num_elements[const_element_id...]
    end

    return support
end

function get_element_vertices(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_element_vertices = get_constituent_element_vertices(space, element_id)
    const_manifold_dim = get_constituent_manifold_dim(space)
    cum_const_manifold_dim = (0, cumsum(const_manifold_dim)...)
    element_vertices = ntuple(manifold_dim) do dim
        const_space_id = findfirst(
            cum_manifold_dim -> dim ≤ cum_manifold_dim, cum_const_manifold_dim[2:end]
        )
        const_dim_id = dim - cum_const_manifold_dim[const_space_id]

        return const_element_vertices[const_space_id][const_dim_id]
    end

    return element_vertices
end

function get_element_lengths(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_element_lengths = get_constituent_element_lengths(space, element_id)
    const_manifold_dim = get_constituent_manifold_dim(space)
    cum_const_manifold_dim = (0, cumsum(const_manifold_dim)...)
    element_lengths = ntuple(manifold_dim) do dim
        const_space_id = findfirst(
            cum_manifold_dim -> dim ≤ cum_manifold_dim, cum_const_manifold_dim[2:end]
        )
        const_dim_id = dim - cum_const_manifold_dim[const_space_id]

        return const_element_lengths[const_space_id][const_dim_id]
    end

    return element_lengths
end

function get_max_local_dim(space::TensorProductSpace)
    return prod(get_max_local_dim, get_constituent_spaces(space))
end

function get_extraction_coefficients(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
    component_id::Int=1,
) where {manifold_dim, num_components, num_patches, num_spaces}
    # The permutations of the constituent spaces should be combined if we allow for more
    # than one component.
    extraction_per_space = get_constituent_extraction(space, element_id)
    extraction_coeffs = kron(
        (extraction_per_space[space][1] for space in num_spaces:-1:1)...
    )

    return extraction_coeffs
end

function get_extraction(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
    component_id::Int=1,
) where {manifold_dim, num_components, num_patches, num_spaces}
    extraction_coeffs = get_extraction_coefficients(space, element_id, component_id)

    return extraction_coeffs, 1:size(extraction_coeffs, 2)
end

function get_local_basis(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
    xi::NTuple{manifold_dim, Vector{Float64}},
    nderivatives::Int=0,
    component_id::Int=1,
) where {manifold_dim, num_components, num_patches, num_spaces}
    const_local_basis = get_constituent_local_basis(space, element_id, xi, nderivatives)
    const_sizes = ntuple(space -> size(const_local_basis[space][1][1][1]), num_spaces)
    num_points = prod(length, xi)
    num_basis = get_num_basis(space, element_id)
    local_basis = Vector{Vector{Vector{Matrix{Float64}}}}(undef, nderivatives + 1)
    for der_order in 0:nderivatives
        num_der_ids = binomial(manifold_dim + der_order - 1, manifold_dim - 1)
        # We assume that there is only one component.
        local_basis[der_order + 1] = [
            [Matrix{Float64}(undef, (num_points, num_basis))] for _ in 1:num_der_ids
        ]
    end

    der_keys = integer_sums(nderivatives, manifold_dim + 1)
    const_manifold_indices = get_constituent_manifold_indices(space)
    const_eval = ntuple(space -> zeros(const_sizes[space]), num_spaces)
    for key in der_keys
        key = key[1:manifold_dim]
        der_order = sum(key)
        der_id = get_derivative_idx(key)
        for space in 1:num_spaces
            space_der_order = sum(key[const_manifold_indices[space]])
            space_der_id = get_derivative_idx(key[const_manifold_indices[space]])
            const_eval[space] .= const_local_basis[space][space_der_order + 1][space_der_id][1]
        end

        if manifold_dim == 1
            local_basis[der_order + 1][der_id][1] = const_eval[1]
        else
            local_basis[der_order + 1][der_id][1] = kron(
                (const_eval[space] for space in num_spaces:-1:1)...
            )
        end
    end

    return local_basis
end

function evaluate(
    space::TensorProductSpace{manifold_dim, num_components, num_patches, num_spaces},
    element_id::Int,
    xi::NTuple{manifold_dim, Vector{Float64}},
    nderivatives::Int=0,
) where {manifold_dim, num_components, num_patches, num_spaces}
    basis_indices = get_basis_indices(space, element_id)
    num_basis = length(basis_indices)
    const_eval = get_constituent_evaluations(space, element_id, xi, nderivatives)
    num_points = prod(length, xi)
    eval = Vector{Vector{Vector{Matrix{Float64}}}}(undef, nderivatives + 1)
    for der_order in 0:nderivatives
        num_der_ids = binomial(manifold_dim + der_order - 1, manifold_dim - 1)
        # We assume that there is only one component.
        eval[der_order + 1] = [
            [Matrix{Float64}(undef, (num_points, num_basis))] for _ in 1:num_der_ids
        ]
    end

    const_manifold_indices = get_constituent_manifold_indices(space)
    der_keys = integer_sums(nderivatives, manifold_dim + 1)
    space_der_order = zeros(Int, num_spaces)
    space_der_id = zeros(Int, num_spaces)
    for key in der_keys
        key = key[1:manifold_dim]
        der_order = sum(key)
        der_id = get_derivative_idx(key)

        for space in 1:num_spaces
            space_der_order[space] = sum(key[const_manifold_indices[space]])
            space_der_id[space] = get_derivative_idx(key[const_manifold_indices[space]])
        end

        if num_spaces == 1
            eval[der_order + 1][der_id][1] =
                const_eval[1][space_der_order[1] + 1][space_der_id[1]][1]
        else
            eval[der_order + 1][der_id][1] = kron(
                (const_eval[space][space_der_order[space] + 1][space_der_id[space]][1] for space in num_spaces:-1:1)...
            )
        end
    end

    return eval, basis_indices
end

# Methods for tensor product B-spline spaces
include("BSplines.jl")
