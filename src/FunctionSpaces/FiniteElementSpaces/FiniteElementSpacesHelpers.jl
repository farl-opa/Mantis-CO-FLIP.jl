"""
    integer_sums(sum_indices::Int, num_indices::Int)

Generates all possible combinations of non-negative integers that sum up
to a given value, where each combination has a specified number of elements.

# Arguments
- `sum_indices::Int`: The target sum of the integers in each combination.
- `num_indices::Int`: The number of integers in each combination.

# Returns
- `::Vector{Vector{Int}}`: Each inner vector represents a combination of
                           integers that sum up to `sum_indices`. If no
                           valid combinations exist, the vectors are empty.
"""
@Memoization.memoize function integer_sums(sum_indices::Int, num_indices::Int)
    solutions = Vector{Vector{Int}}(undef,0)
    if num_indices == 1
        push!(solutions, [sum_indices])
    elseif num_indices > 1
        for combo in Combinatorics.combinations(0:sum_indices+num_indices-2, num_indices-1)
            s = zeros(Int, num_indices)
            s[1] = combo[1]
            for i in 2:num_indices-1
                s[i] = combo[i] - combo[i-1] - 1
            end
            s[end] = sum_indices+num_indices-2 - combo[num_indices-1]
            push!(solutions, s)
        end
    else
        throw(ArgumentError("Number of indices must be greater than 0."))
    end

    return solutions
end

"""
    ordered_to_linear_index(ord_ind::Vector{Int}, max_ind::Vector{Int})

Convert given ordered pair index to linear index.

# Arguments
- `ord_ind::Vector{Int}`: ordered index pair
- `max_ind::Vector{Int}`: maximum index in each direction

# Returns
- `::Int`: computed linear index
"""
function ordered_to_linear_index(ord_ind::Vector{Int}, max_ind::Vector{Int})
    m = length(max_ind)
    @assert length(ord_ind)==m "Dimension mismatch."
    max_ind = cumprod(max_ind)
    lin_ind = ord_ind[1]
    for i = 2:m
        lin_ind += (ord_ind[i]-1)*max_ind[i-1]
    end
    return lin_ind
end

# Additional method overloads for ordered_to_linear_index
function ordered_to_linear_index(ord_ind::NTuple{m,Int}, max_ind::NTuple{m,Int}) where {m} #TODO Are the methods for vector inputs ever being used? Or should we just stick to Tuples?
    return ordered_to_linear_index(collect(ord_ind), collect(max_ind))
end

function ordered_to_linear_index(ord_ind::Vector{Int}, max_ind::NTuple{m,Int}) where {m} #TODO Are the methods for vector inputs ever being used? Or should we just stick to Tuples?
    return ordered_to_linear_index(ord_ind, collect(max_ind))
end

"""
    linear_to_ordered_index(lin_ind::Int, max_ind::Vector{Int})

Convert given linear index to ordered index pair.

# Arguments
- `lin_ind::Int`: computed linear index
- `max_ind::Vector{Int}`: maximum index in each direction

# Returns
- `::Vector{Int}`: ordered index pair
"""
function linear_to_ordered_index(lin_ind::Int, max_ind::Vector{Int})
    m = length(max_ind)
    ord_ind = zeros(Int,m)
    ord_ind[1] = lin_ind - floor((lin_ind-1)/max_ind[1])*max_ind[1]
    lin_ind = (lin_ind - ord_ind[1])/max_ind[1]
    for i = 2:m
        ord_ind[i] = lin_ind - floor(lin_ind/max_ind[i])*max_ind[i] + 1
        lin_ind = (lin_ind - ord_ind[i] + 1)/max_ind[i]
    end
    @assert lin_ind==0 "Ordered pair computation failed."
    return Int.(ord_ind)
end

# Additional method overloads for linear_to_ordered_index
function linear_to_ordered_index(lin_ind::Int, max_ind::NTuple{m,Int}) where {m} #TODO Are the methods for vector inputs ever being used? Or should we just stick to Tuples?
    return linear_to_ordered_index(lin_ind, collect(max_ind))
end

"""
    get_derivative_idx(der_key::Vector{Int})

Convert the given derivative key to a linear index corresponding to its storage location.

If `local_basis` corresponds to basis evaluations for some `manifold_dim`-variate function space,
then its `k`-th derivatives will all be stored in the location `local_basis[k+1]`.
Moreover, the `k`-th derivative corresponding to the key `[i₁,i₂,...,iₙ]` in the
location `local_basis[k+1][m]` where:
- `m = 1` when `iⱼ = 0` for all `j`, i.e., for basis function values;
- `m = 1+r` when `iⱼ = 0` for all `j` except for `j = r` and `iⱼ = 1`, i.e.,
  for the first derivative w.r.t. the `j`-th canonical coordinate;
- in all other cases (i.e., when `k>1`),  the value of `m` is equal to `l`
  if `[i₁,i₂,...,iₙ]` is the `l`-th key returned by the function `integer_sums(k, manifold_dim)`.

As an example, consider the first derivative with respect to x₁ (∂/∂x₁)
in 2D, which has key [1, 0]. In 3D, this same derivative has key
[1, 0, 0]. In 3D, the derivative ∂³/∂x₁²∂x₂ thus has key [2, 1, 0].

# Arguments
- `der_key::Vector{Int}`: A key for the desired derivative order.

# Returns
- `::Int`: The linear index corresponding to the derivative's storage location in basis evaluations.
"""
@Memoization.memoize Dict function get_derivative_idx(der_key::Vector{Int})
    if any(der_key .< 0)
        throw(ArgumentError("Derivative key $der_key is not valid!"))
    end

    if sum(der_key) == 0
        # Request 0th order derivatives, i.e., evaluation of the function
        derivative_idx = 1

    elseif sum(der_key) == 1
        # Request first derivatives

        # Trivial indexing: the linear index associated to the input key der_key is just the
        # index of the value with the 1 (the first derivative we wish)
        derivative_idx = findfirst(x -> x == 1, der_key)

    else
        # Request derivatives with order higher than 1

        # Generate all valid derivative keys
        all_keys = integer_sums(sum(der_key),length(der_key))

        # Find the linear index associated to the input key der_key
        derivative_idx = findfirst(x -> x == der_key, all_keys)

        # Throw and error if the derivative key does not exist/is not valid
        if isnothing(derivative_idx)
            throw(ArgumentError("Derivative key $der_key is not valid!"))
        end
    end

    return derivative_idx
end
