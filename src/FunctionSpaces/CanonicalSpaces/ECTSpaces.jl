import SparseArrays, LinearAlgebra

abstract type AbstractECTSpaces <: AbstractCanonicalSpace end

@doc raw"""
    struct GeneralizedTrigonometric <: AbstractECTSpaces

Concrete type for Generalized Trignometric section space spanned by `<1, x, ..., x^(p-2), cos(wx), sin(wx)>` on `[0,1]`.

# Fields
- `p::Int`: Degree of the space.
- `w::Float64`: Weight parameter for the space.
- `t::Bool`: flag to indicate if critical length is exceeded.
- `m::Int`: number of terms from the infinite sum used to build the basis.
- `C::Matrix{Float64}`: representation matrix for the local basis.
"""
struct GeneralizedTrigonometric <: AbstractECTSpaces
    p::Int
    w::Float64
    t::Bool
    m::Int
    C::Matrix{Float64}
    endpoint_tol::Float64
    function GeneralizedTrigonometric(p::Int)
        w = 1.0
        t = false
        m = 10
        GeneralizedTrigonometric(p, w, t, m)
    end

    function GeneralizedTrigonometric(p::Int, w::Float64)
        t = abs(w) >= 3.0
        m = 10
        GeneralizedTrigonometric(p, w, t, m)
    end

    function GeneralizedTrigonometric(p::Int, w::Float64, t::Bool)
        m = 10
        GeneralizedTrigonometric(p, w, t, m)
    end

    function GeneralizedTrigonometric(p::Int, w::Float64, t::Bool, m::Int)
        endpoint_tol = 1e-12
        new(p, w, t, m, gtrig_representation(p, w, t, m),endpoint_tol)
     end
end

@doc raw"""
    evaluate(gtrig::GeneralizedTrigonometric, ξ::Vector{Float64}, nderivatives::Int64)

Compute derivatives up to order `nderivatives` for all basis functions of degree `p` at `ξ` for ``\xi \in [0.0, 1.0]``. 

# Arguments
- `gtrig::GeneralizedTrigonometric`: Generalized Trigonometric section space.
- `ξ::Vector{Float64}`: vector of evaluation points ``\in [0.0, 1.0]``.
- `nderivatives::Int64`: maximum order of derivatives to be computed (nderivatives ``\leq p``).

See also [`evaluate(gtrig::GeneralizedTrigonometric, ξ::Float64, nderivatives::Int64)`](@ref).
"""
function evaluate(gtrig::GeneralizedTrigonometric, ξ::Vector{Float64}, nderivatives::Int)
    neval = length(ξ)
    # allocate space for derivatives
    # - ders[j+1][1] contains the matrix of evaluations of the j-th derivative
    ders = Vector{Vector{Matrix{Float64}}}(undef, nderivatives + 1)
    for j = 0:nderivatives
        ders[j+1] = Vector{Matrix{Float64}}(undef, 1)
        ders[j+1][1] = zeros(Float64, neval, gtrig.p + 1)
    end
    # loop over the evaluation points and evaluate all derivatives at each point
    for i = 1:neval
        tmp = _evaluate(gtrig, ξ[i],nderivatives)
        for j = 0:nderivatives
            ders[j+1][1][i,:] .= tmp[1,:,j+1]
        end
    end
    return ders
end

function _evaluate(gtrig::GeneralizedTrigonometric, xi::Float64, nderivatives::Int)
    tol = gtrig.endpoint_tol
    M = zeros(Float64, 1, gtrig.p+1, nderivatives+1)
    j = xi >= 0.0 && xi < 1.0
    if abs(xi-1.0) < tol
       j = true
    end
    if j
        if gtrig.t
            for r = 0:nderivatives
                k = min(r, gtrig.p-1)
                wxl = gtrig.w * xi
                E = [1.0; cumprod((1.0 ./ (1:gtrig.p-k)) * wxl)]
                if mod(r, 4) == 0
                    E[gtrig.p-k] = cos(wxl);
                    E[gtrig.p+1-k] = sin(wxl);
                elseif mod(r, 4) == 1
                    E[gtrig.p-k] = -sin(wxl);
                    E[gtrig.p+1-k] = cos(wxl);
                elseif mod(r, 4) == 2
                    E[gtrig.p-k] = -cos(wxl);
                    E[gtrig.p+1-k] = -sin(wxl);
                elseif mod(r, 4) == 3
                    E[gtrig.p-k] = sin(wxl);
                    E[gtrig.p+1-k] = -cos(wxl);
               end
                M[1, :, r+1] = (gtrig.w^r) * (gtrig.C[:,k+1:end] * E)
            end
        else
            for r = 0:nderivatives
                k = min(r, gtrig.p-1)
                ww = [1; cumprod(repeat([-gtrig.w * gtrig.w], gtrig.m))]
                Ef = [1.0; cumprod((1.0 ./ (1:gtrig.p-k+2*gtrig.m)) * xi)]
                E = Ef[1:gtrig.p+1-k]
                E[gtrig.p-k, :] = Ef[gtrig.p-k:2:end, :]' * ww
                E[gtrig.p-k+1, :] = Ef[gtrig.p-k+1:2:end, :]' * ww
                M[1, :, r+1] = gtrig.C[:,k+1:end] * E
            end
        end
    end
    return M
end

@doc raw"""
    evaluate(gtrig::GeneralizedTrigonometric, ξ::Vector{Float64})

Compute all basis function values at `ξ` in ``[0.0, 1.0]``.

# Arguments
- `gtrig::GeneralizedTrigonometric`:  Generalized Trigonometric section space.
- `xi::Vector{Float64}`: vector of evaluation points ``\in [0.0, 1.0]``.

See also [`evaluate(gtrig::GeneralizedTrigonometric, xi::Vector{Float64}, nderivatives::Int64)`](@ref).
"""
function evaluate(gtrig::GeneralizedTrigonometric, xi::Vector{Float64})
    return evaluate(gtrig, xi, 0)
end

function evaluate(gtrig::GeneralizedTrigonometric, xi::Float64)
    return evaluate(gtrig, [xi], 0)
end


@doc raw"""
    gtrig_representation(p::Int, w::Float64, t::Bool, m::Int)

Build representation matrix for Generalized Trignometric section space of degree `p`, weight `w` and `m` terms.

# Arguments
- `p::Int`: Degree of the space.
- `w::Float64`: Weight parameter for the space.
- `t::Bool`: flag to indicate if critical length is exceeded.
- `m::Int`: number of terms from the infinite sum used to build the basis.

# Returns:
- `C::Matrix{Float64}`: representation matrix for the local basis.
"""

import LinearAlgebra, ToeplitzMatrices

function gtrig_representation(p::Int, w::Float64, t::Bool, m::Int)
    
    I = Matrix(1.0LinearAlgebra.I, p+1, p+1)
    if t
        cw = cos(w)
        sw = sin(w)
        M0 = I[:,:]
        M0[[p, p+1], [p, p+1]] = 0.0
        M0[p, 1:4:end] .= 1
        M0[p, 3:4:end] .= -1
        M0[p+1, 2:4:end] .= 1
        M0[p+1, 4:4:end] .= -1
        M1 = ToeplitzMatrices.Toeplitz([1; cumprod(w ./ (1:p))], reshape(I, ((p+1)^2,)))
        M1[p, 1:4:end] .= cw
        M1[p, 2:4:end] .= -sw
        M1[p, 3:4:end] .= -cw
        M1[p, 4:4:end] .= sw
        M1[p+1, 1:4:end] .= sw
        M1[p+1, 2:4:end] .= cw
        M1[p+1, 3:4:end] .= -sw
        M1[p+1, 4:4:end] .= -cw
    else
        M0 = I[:,:]
        ww = [1 cumprod(repeat([-w * w], 1, m), dims=2)]
        M = ToeplitzMatrices.Toeplitz([1; cumprod(1.0 ./ (1:p+2*m))], reshape(I, ((p+1)^2,)))
        M1 = M[1:p+1, :]
        M1[p, :] = ww * M[p:2:end, :]
        M1[p+1, :] = ww * M[p+1:2:end, :]
    end
    cs = zeros(Float64, p+1)
    C = zeros(Float64, p+1, p+1)
    C[p+1, :] =  [M1[:, 1] M0[:, 1:p]]' \ [1.0; zeros(p)]
    for i = 2:p
        cs = cs + C[p+1-i+2, :]
        cc = zeros(Float64, p+1)
        cc[i] = -cs' * M1[:, i]
        C[p+1-i+1, :] = [M1[:, 1:i] M0[:, 1:p+1-i]]' \ cc
    end
    C[1, :] = [M0[:, 1] M1[:, 1:p]]' \ [1.0; zeros(p)] 

    return C
end

"""
    build_two_scale_matrix(ect_space::AbstractECTSpaces, num_sub_elements::Int)

Uniformly subdivides the ECT space into `num_sub_elements` sub-elements. It is assumed that `num_sub_elements` is a power of 2, else the method throws an argument error. It returns a global subdivision matrix that maps the global basis functions of the ECT space to the global basis functions of the subspaces.

# Arguments
- `ect_space::AbstractECTSpaces`: A ect space.
- `num_sub_elements::Int`: The number of subspaces to divide the EC T space into.

# Returns
- `::SparseMatrixCSC{Float64}`: A global subdivision matrix that maps the global basis functions of the ECT space to the global basis functions of the subspaces.
"""
function build_two_scale_matrix(ect_space::AbstractECTSpaces, num_sub_elements::Int)
    num_ref = log2(num_sub_elements)
    if num_sub_elements < 2 || !isapprox(num_ref-round(num_ref), 0.0, atol=1e-12)
        throw(ArgumentError("Number of subdivisions should be a power of 2 and greater than 1"))
    end
    p = get_polynomial_degree(ect_space)
    num_ref = Int(num_ref)
    
    # first, build the subdivision matrix for num_sub_elements = 2
    bisection_matrix = zeros(Float64, 2 * (p+1), p+1)
    # evaluate all ECT basis functions at left and right endpoints
    eval_L = _evaluate_all_at_point(ect_space, 0.0, p-1)
    # fill the subdivision matrix by solving endpoint interpolation problems
    for i = 1:p
        if i == 1
            bisection_matrix[1, i] = 1.0
        else
            for j = 1:i-1
                bisection_matrix[i,j] = (eval_L[j, i]/2^(i-1) - sum(bisection_matrix[1:i-1, j] .* eval_L[1:i-1, i]))/eval_L[i, i]
            end
            bisection_matrix[i,i] = 1.0 - sum(bisection_matrix[i, 1:i-1])
        end
    end
    # evaluate at midpoint for the central degree of freedom
    eval_M = _evaluate_all_at_point(ect_space, 0.5, 0)
    bisection_matrix[p+1, 1:p+1] = eval_M
    # fill the rest of the matrix in symmetrically
    bisection_matrix[p+2:end, :] = bisection_matrix[p+1:-1:1, p+1:-1:1]
    # convert to a sparse matrix
    bisection_matrix = SparseArrays.sparse(bisection_matrix)

    # now, build the subdivision matrix for num_sub_elements > 2
    subdivision_matrix = SparseArrays.sparse(Matrix(LinearAlgebra.I, p+1, p+1))
    for i = 1:num_ref
        subdivision_matrix = SparseArrays.blockdiag([bisection_matrix for i = 1:2^(i-1)]...) * subdivision_matrix
    end
    
    return subdivision_matrix
end